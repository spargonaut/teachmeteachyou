plugins {
    id 'groovy'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
    id 'org.flywaydb.flyway' version '3.2'
}

shadowJar {
    archiveName = 'LOTApplication.jar'
}

ext {
    dropwizardVersion = '0.9.1'
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/cognitiveConnections'
    user = 'postgres'
    password = ''
    schemas = ['public']
    baselineOnMigrate = true
}

sourceSets {
    integrationTest {
        groovy {
            srcDir 'src/integrationTest/groovy'
        }
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    compile "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-assets:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-jdbi:${dropwizardVersion}"
    compile 'org.postgresql:postgresql:9.4-1204-jdbc42'
    compile 'com.google.inject:guice:4.0'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.springframework.boot:spring-boot:1.3.1.RELEASE'

    integrationTestCompile sourceSets.main.output
    integrationTestCompile configurations.testCompile
    integrationTestCompile sourceSets.test.output
    integrationTestRuntime configurations.testRuntime
}

task runLocal(dependsOn: jar, type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.spargonaut.LOTApplication'
    args 'server', 'LOTConfiguration.yml'
}

task integrationTest(type: Test, dependsOn: build) {
    description 'Runs the integration tests'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}